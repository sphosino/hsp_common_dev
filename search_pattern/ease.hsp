#ifndef ease
//最小単位easeオブジェクト
#module ease from, to, type, easetime

#modinit double _from, double _to, int _type, local thismod_ID
	set_ease_obj thismod, _from, _to, _type, 1.0
	mref thismod_ID, 2
	return thismod_ID
	
#modfunc set_ease_time double t
	if t <= 0.0: return
	easetime = t
	return

#modfunc set_ease_obj double _from, double _to, int _type, double t
	from = _from
	to = _to
	type = _type
	set_ease_time thismod, t
	return

#modfunc reverse_ease
	tmp = from
	from = to
	to = tmp
	return
	
#modcfunc _get_ease_value double t
	setease from, to, type
	return geteasef(t / easetime)
	
#modcfunc get_ease_info array a
	a = from, to, easetime
	return type
	
#global


//複数のeaseを束ねたオブジェクト
#module ease_obj x, y, sx, sy, a, r, u

#modinit int posx, int posy, int sizex, int sizey, local thismod_ID
	// 最初はその場に動かないオブジェクトを作成
	newmod x, ease, posx, posx, ease_linear
	newmod y, ease, posy, posy, ease_linear
	newmod sx, ease, sizex, sizex, ease_linear
	newmod sy, ease, sizey, sizey, ease_linear
	newmod a, ease, 256, 256, ease_linear
	newmod r, ease, 0, 0, ease_linear
	newmod u, ease, 0, 0, ease_linear
	mref thismod_ID, 2
	return thismod_ID
	
#modfunc _set_easetime_all double time
	set_ease_time x, time
	set_ease_time y, time
	set_ease_time sx, time
	set_ease_time sy, time
	set_ease_time a, time
	set_ease_time r, time
	set_ease_time u, time
	return
	
#modfunc _set_ease_posx double from, double to, int type, double t
	set_ease_obj x, from, to, type, t
	return
#modfunc _set_ease_posy double from, double to, int type, double t
	set_ease_obj y, from, to, type, t
	return
#modfunc _set_ease_sizex double from, double to, int type, double t
	set_ease_obj sx, from, to, type, t
	return
#modfunc _set_ease_sizey double from, double to, int type, double t
	set_ease_obj sy, from, to, type, t
	return
#modfunc _set_ease_alpha double from, double to, int type, double t
	set_ease_obj a, from, to, type, t
	return
#modfunc _set_ease_angle double from, double to, int type, double t
	set_ease_obj r, from, to, type, t
	return
#modfunc _set_ease_custom double from, double to, int type, double t
	set_ease_obj u, from, to, type, t
	return
	
#modcfunc _get_ease_posx double t
	return _get_ease_value(x, t)
#modcfunc _get_ease_posy double t
	return _get_ease_value(y, t)
#modcfunc _get_ease_sizex double t
	return _get_ease_value(sx, t)
#modcfunc _get_ease_sizey double t
	return _get_ease_value(sy, t)
#modcfunc _get_ease_alpha double t
	return _get_ease_value(a, t)
#modcfunc _get_ease_angle double t
	return _get_ease_value(r, t)
#modcfunc _get_ease_custom double t
	return _get_ease_value(u, t)
	
#global


//draw_objectがつかうモジュール
#module ease_obj_manager

#defcfunc make_ease_obj int x, int y, int sx, int sy
	newmod data,ease_obj, x, y, sx, sy
	return stat

#deffunc set_easetime_all int id, double time
	_set_easetime_all data(id), time
	return

//マクロで定義easetimeを個別で指定可能に0.0なら設定を変更しない
#define global set_ease_posx(%1,%2,%3,%4 = 0, %5 = 0.0) _set_ease_posx data@ease_obj_manager(%1),%2,%3,%4,%5
#define global set_ease_posy(%1,%2,%3,%4 = 0, %5 = 0.0) _set_ease_posy data@ease_obj_manager(%1),%2,%3,%4,%5
#define global set_ease_sizex(%1,%2,%3,%4 = 0, %5 = 0.0) _set_ease_sizex data@ease_obj_manager(%1),%2,%3,%4,%5
#define global set_ease_sizey(%1,%2,%3,%4 = 0, %5 = 0.0) _set_ease_sizey data@ease_obj_manager(%1),%2,%3,%4,%5
#define global set_ease_alpha(%1,%2,%3,%4 = 0, %5 = 0.0) _set_ease_alpha data@ease_obj_manager(%1),%2,%3,%4,%5
#define global set_ease_angle(%1,%2,%3,%4 = 0, %5 = 0.0) _set_ease_angle data@ease_obj_manager(%1),%2,%3,%4,%5
#define global set_ease_custom(%1,%2,%3,%4 = 0, %5 = 0.0) _set_ease_custom data@ease_obj_manager(%1),%2,%3,%4,%5

#defcfunc is_entity_id_alive int id
	return varuse(data(id))

#defcfunc get_ease_posx int id, double t
	return _get_ease_posx(data(id), t)
#defcfunc get_ease_posy int id, double t
	return _get_ease_posy(data(id), t)
#defcfunc get_ease_sizex int id, double t
	return _get_ease_sizex(data(id), t)
#defcfunc get_ease_sizey int id, double t
	return _get_ease_sizey(data(id), t)
#defcfunc get_ease_alpha int id, double t
	return _get_ease_alpha(data(id), t)
#defcfunc get_ease_angle int id, double t
	return _get_ease_angle(data(id), t)
#defcfunc get_ease_custom int id, double t
	return _get_ease_custom(data(id), t)
	
#deffunc del_ease_obj int id
	delmod data(id)
	return

#global
#endif